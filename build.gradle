// plugins 不能直接写到 subprojects里面，要先在这里声明，然后在 subprojects 中使用 apply 引入
plugins {
	id 'org.springframework.boot' version '2.3.0.RELEASE' apply false
	id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
	//id 'java'
}

allprojects {
	group = 'org.huang'
	version = '0.0.1-SNAPSHOT'
}

repositories {
	//mavenCentral()
	maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
}

subprojects  {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'
    
    sourceCompatibility = '1.8'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		//mavenCentral()
		maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
	}

	ext {
		set('springCloudVersion', "Hoxton.SR4")
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.cloud:spring-cloud-starter'
		implementation 'org.slf4j:slf4j-api:1.7.28'

		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		compileOnly  'org.projectlombok:lombok'
		annotationProcessor  'org.projectlombok:lombok'
		testImplementation ('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
	test {
		useJUnitPlatform()
	}
}